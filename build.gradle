buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.10'
	}
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'signing'

repositories {
	mavenCentral()
}

group 'net.netnook.repeg'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	jmh {
		compileClasspath += sourceSets.main.runtimeClasspath
		compileClasspath += sourceSets.test.runtimeClasspath
	}
}

dependencies {
	testImplementation group: 'junit', name: 'junit', version: '4.11'
	testImplementation group: 'org.assertj', name: 'assertj-core', version: '1.7.0'
	testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.7.8'

	jmhImplementation group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.21'
	jmhImplementation group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.21'
}

task jmh(type: JavaExec, dependsOn: jmhClasses) {
	main = 'org.openjdk.jmh.Main'
	classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
}

javadoc {
	options.overview = "src/main/java/overview.html"
}

test {
	testLogging {
		events "passed", "skipped", "failed"
	}
}

asciidoctor {
	sourceDir = file('docs')
	sources {
		include 'index.adoc'
	}
	outputDir = file('build/docs')
	attributes 'toc': 'left',
			'numbered': true,
			'icons': 'font',
			'source-highlighter': 'prettify',
			'stylesdir': 'resources',
			'stylesheet': 'style.css',
			'linkcss': true

	resources {
		from 'docs/resources'
		into 'resources'
	}
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

publishing {
	publications {
		repeg(MavenPublication) {
			artifactId = 'repeg'

			from components.java
			artifact tasks.sourcesJar
			artifact tasks.javadocJar

			pom {
				name = "${groupId}:${artifactId}".toString()
				description = 'PEG based parser builder'
				url = 'https://netnook.github.io/repeg/'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						name = 'Andrew Fowler'
						email = 'andrew.fowler@netnook.net'
					}
				}
				scm {
					connection = 'scm:git:git@github.com:netnook/repeg.git'
					developerConnection = 'scm:git:git@github.com:netnook/repeg.git'
					url = 'https://github.com/netnook/repeg'
				}
			}
		}
	}

	if (project.hasProperty('ossrhUsername')) {
		repositories {
			maven {
				name = 'ossrhSnapshots'
				url = "https://oss.sonatype.org/content/repositories/snapshots/"
				credentials {
					username project.ossrhUsername
					password project.ossrhPassword
				}
			}
			maven {
				name = 'ossrhStaging'
				url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
				credentials {
					username project.ossrhUsername
					password project.ossrhPassword
				}
			}
		}
	}
}

signing {
	useGpgCmd()
	sign publishing.publications.repeg
}
